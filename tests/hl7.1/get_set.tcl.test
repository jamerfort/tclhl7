#!/usr/bin/env tclsh

package require tcltest

source tests/customMatchers.tcl
source hl7.1/hl7.tcl

namespace eval HL7 {
	proc test_hl7 {name description msgdata desired_msgdata hl7 cmd args} {
		tcltest::test $name $description -body {
			set msg [hl7 parse [join $msgdata \n] \n]
			set desired_msg [hl7 parse [join $desired_msgdata \n] \n]

			set rslt [eval {hl7 $cmd $msg} $args]

			if {[hl7 data $rslt] == [hl7 data $desired_msg]} {
				return 1
			} else {
				return 0
			}
		} -result 1

		if { [info exists ::env(SHOW_QUERY_RESULTS)] && $::env(SHOW_QUERY_RESULTS) } {
			puts "   - [string map {\n "\n     "} [hl7 data $rslt]]"
			puts "   - [string map {\n "\n     "} [hl7 data $desired_msg]]"
		}
	}

	proc testget {name description message query results} {
		tcltest::test $name $description -body {
			set msg [Message::parse [join $message \r]]
			set rslts [GetSet::get $msg $query]
			return $rslts
		} -result $results

		if { [info exists ::env(SHOW_QUERY_RESULTS)] && $::env(SHOW_QUERY_RESULTS) } {
			puts "\t$description"
			puts "\t$query => $rslts"
		}
	}

	proc testset {name description message query value} {
		
		tcltest::test $name $description -body {
			set msg [Message::parse [join $message \r]]
			
			set before [GetSet::get $msg $query]
			set after [GetSet::get [GetSet::_set $msg $query $value] $query]

			set before_count [llength $before]
			set after_count [llength $after]

			if { $before_count != $after_count } {
				return "$before_count != $after_count"
			}

			for {set i 0} {$i < $after_count} {incr i} {
				set before_address [lindex $before $i 1]

				set after_value [lindex $after $i 0]
				set after_address [lindex $after $i 1]

				if { $before_address != $after_address } {
					return "$before_address != $after_address"
				}

				if { $after_value != $value } {
					return "$after_value != $value"
				}
			}

			return 1
		} -result 1

		if { [info exists ::env(SHOW_QUERY_RESULTS)] && $::env(SHOW_QUERY_RESULTS) } {
			puts "\t$description"
			puts "\t$query => $value"
			
			set msg [Message::parse [join $message \n]]
			set msg [hl7 set $msg $query $value]
			puts "\t\t[string map {\n \n\t\t} [hl7 data [hl7 set $msg $query $value]]]"
		}
	}

	set testmsg {
		{MSH|^~\&|}
		{PID|||1234567~abcdefg}
		{TST||This&is^a subcomponent~Not another&test|A^B^C^D~E^F^G}
		{PID|||1234567}
	}

	testget get-1.0 {Test segment queries on segment type} $testmsg "PID" {{{PID {} {} {1234567 abcdefg}} 1} {{PID {} {} 1234567} 3}}
	testget get-1.0 {Test segment queries on segment index} $testmsg "2" {{{TST {} {{{This is} {{a subcomponent}}} {{{Not another} test}}} {{A B C D} {E F G}}} 2}}
	testget get-1.0 {Test segment queries on both segment type and index} $testmsg "MSH,2" {{{MSH | {^~\&} {}} 0} {{TST {} {{{This is} {{a subcomponent}}} {{{Not another} test}}} {{A B C D} {E F G}}} 2}}

	testget get-1.0 {Test field queries} $testmsg "PID.3" {{{1234567 abcdefg} 1.3} {1234567 3.3}}
	testget get-1.0 {Test field queries outside of range} $testmsg "PID.7" {{{} 1.7} {{} 3.7}}
	testget get-1.0 {Test field queries with *} $testmsg "PID.*" {{PID 1.0} {{} 1.1} {{} 1.2} {{1234567 abcdefg} 1.3} {PID 3.0} {{} 3.1} {{} 3.2} {1234567 3.3}}
	testget get-1.0 {Test field queries with ranges} $testmsg "PID.2-3" {{{} 1.2} {{1234567 abcdefg} 1.3} {{} 3.2} {1234567 3.3}}
	testget get-1.0 {Test field queries with ranges (out of range)} $testmsg "1.2-7" {{{} 1.2} {{1234567 abcdefg} 1.3} {{} 1.4} {{} 1.5} {{} 1.6} {{} 1.7}}

	testget get-1.0 {Test repetition queries} $testmsg "PID.3.0" {{1234567 1.3.0} {1234567 3.3.0}}
	testget get-1.0 {Test repetition queries with *} $testmsg "PID.3.*" {{1234567 1.3.0} {abcdefg 1.3.1} {1234567 3.3.0}}
	testget get-1.0 {Test repetition queries outside range} $testmsg "PID.3.4" {}

	testget get-1.0 {Test component queries} $testmsg "TST.3.0.1" {{B 2.3.0.1}}
	testget get-1.0 {Test component queries with *} $testmsg "TST.3.0.*" {{A 2.3.0.0} {B 2.3.0.1} {C 2.3.0.2} {D 2.3.0.3}}

	testget get-1.0 {Test subcomponent queries} $testmsg "2.2.0.0.1" {{is 2.2.0.0.1}}
	testget get-1.0 {Test subcomponent queries with *} $testmsg "TST.2.0.0.*" {{This 2.2.0.0.0} {is 2.2.0.0.1}}




	testset set-1.0 {Test segment sets} $testmsg "PID" [list PID {} A B C D]
	testset set-1.0 {Test field sets} $testmsg "PID.3" "ABCDE"
	testset set-1.0 {Test repetition sets} $testmsg "PID.3.*" "ABCDE"
	testset set-1.0 {Test component sets} $testmsg "PID.3.*.0" "ABCDE"
	testset set-1.0 {Test subcomponent sets} $testmsg "PID.3.*.0.0" "ABCDE"






	test_hl7 set-1.0 {Test adding past existing field} {
			{MSH|^~\&|...}
			{PID|1}
		} {
			{MSH|^~\&|...}
			{PID|1||||^TEST}
		} hl7 set "PID.5.0.1.0" "TEST"



	tcltest::test get_reverse-1.0 {Test getting the reverse of a query} -body {
		set msg [Message::parse [join $testmsg \r]]
		return [GetSet::get_reverse $msg "TST.3.0.*"]
	} -result {{D 2.3.0.3} {C 2.3.0.2} {B 2.3.0.1} {A 2.3.0.0}}




	tcltest::test get_values-1.0 {Test getting the values of a query} -body {
		set msg [Message::parse [join $testmsg \r]]
		return [GetSet::get_values $msg "TST.3.0.*"]
	} -result {A B C D}

	tcltest::test get_values-1.0 {Test getting the reversed values of a query} -body {
		set msg [Message::parse [join $testmsg \r]]
		return [GetSet::get_values $msg "TST.3.0.*" 1]
	} -result {D C B A}





	test_hl7 clear-1.0 {Test clearing} {
			{MSH|^~\&|...}
			{PID|1||||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||||^}
		} hl7 clear "PID.5.0.1.0"





	test_hl7 add-1.0 {Test adding a segment} {
			{MSH|^~\&|...}
			{PID|1||||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||||^TEST}
			{TST|This is a test segment}
		} hl7 add "" {TST {{{{This is a test segment}}}}}

	tcltest::test add-1.0 {Test that adding a field errors out} -body {
		set msg [hl7 parse [join {
				{MSH|^~\&|...}
				{PID|1||||^TEST}
			} "\r"]]

		hl7 add $msg "PID" TST
	} -returnCodes error -result  "ERROR: hl7 add should not be run on segments or subcomponents."

	test_hl7 add-1.0 {Test adding a repetition} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||ABC~TST||^TEST}
		} hl7 add "PID.3" TST

	test_hl7 add-1.0 {Test adding a component} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||ABC^TST||^TEST}
		} hl7 add "PID.3.0" TST

	test_hl7 add-1.0 {Test adding a subcomponent} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||ABC&TST||^TEST}
		} hl7 add "PID.3.0.0" TST

	tcltest::test add-1.0 {Test that adding deeper than a subcomponent errors out} -body {
		set msg [hl7 parse [join {
				{MSH|^~\&|...}
				{PID|1||||^TEST}
			} "\r"]]

		hl7 add $msg "PID.3.0.0.0" TST
	} -returnCodes error -result  "ERROR: hl7 add should not be run on segments or subcomponents."






	test_hl7 insert-1.0 {Test inserting a segment} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{TST}
			{PID|1||ABC||^TEST}
		} hl7 insert "PID" TST

	tcltest::test insert-1.0 {Test inserting a field errors out} -body {
		set msg [hl7 parse [join {
				{MSH|^~\&|...}
				{PID|1||||^TEST}
			} "\r"]]

		hl7 insert $msg "PID.0" TST
	} -returnCodes error -result  "ERROR: hl7 insert should not be run on fields."

	test_hl7 insert-1.0 {Test inserting a repetition} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||TST~ABC||^TEST}
		} hl7 insert "PID.3.0" TST

	test_hl7 insert-1.0 {Test inserting a component} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TST^TEST}
		} hl7 insert "PID.5.0.1" TST

	test_hl7 insert-1.0 {Test inserting a subcomponent} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||TST&ABC||^TEST}
		} hl7 insert "PID.3.0.0.0" TST






	test_hl7 insert_before-1.0 {Test inserting before a segment} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{TST}
			{PID|1||ABC||^TEST}
		} hl7 insert_before "PID" TST

	tcltest::test insert_before-1.0 {Test inserting before a field errors out} -body {
		set msg [hl7 parse [join {
				{MSH|^~\&|...}
				{PID|1||||^TEST}
			} "\r"]]

		hl7 insert_before $msg "PID.0" TST
	} -returnCodes error -result  "ERROR: hl7 insert should not be run on fields."

	test_hl7 insert_before-1.0 {Test inserting before a repetition} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||TST~ABC||^TEST}
		} hl7 insert_before "PID.3.0" TST

	test_hl7 insert_before-1.0 {Test inserting before a component} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TST^TEST}
		} hl7 insert_before "PID.5.0.1" TST

	test_hl7 insert_before-1.0 {Test inserting before a subcomponent} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||TST&ABC||^TEST}
		} hl7 insert_before "PID.3.0.0.0" TST






	test_hl7 insert_after-1.0 {Test inserting after a segment} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
			{TST}
		} hl7 insert_after "PID" TST

	tcltest::test insert_after-1.0 {Test inserting after a field errors out} -body {
		set msg [hl7 parse [join {
				{MSH|^~\&|...}
				{PID|1||||^TEST}
			} "\r"]]

		hl7 insert_after $msg "PID.0" TST
	} -returnCodes error -result  "ERROR: hl7 insert should not be run on fields."

	test_hl7 insert_after-1.0 {Test inserting after a repetition} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||ABC~TST||^TEST}
		} hl7 insert_after "PID.3.0" TST

	test_hl7 insert_after-1.0 {Test inserting after a component} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST^TST}
		} hl7 insert_after "PID.5.0.1" TST

	test_hl7 insert_after-1.0 {Test inserting after a subcomponent} {
			{MSH|^~\&|...}
			{PID|1||ABC||^TEST}
		} {
			{MSH|^~\&|...}
			{PID|1||ABC&TST||^TEST}
		} hl7 insert_after "PID.3.0.0.0" TST





	tcltest::test each-1.0 {Test each with values and addresses} -body {
		set values {}
		set addresses {}

		set msg [hl7 parse [join {
			{MSH|^~\&|...}
			{PID|1||ABC~TST~HEY||^TEST}
		} "\r"]]

		hl7 each {v a} $msg "PID.3.*.0.0" {
			lappend values $v
			lappend addresses $a
		}

		return [list $values $addresses]
	} -result {{ABC TST HEY} {1.3.0.0.0 1.3.1.0.0 1.3.2.0.0}}

	tcltest::test each-1.0 {Test each with just values} -body {
		set values {}

		set msg [hl7 parse [join {
			{MSH|^~\&|...}
			{PID|1||ABC~TST~HEY||^TEST}
		} "\r"]]

		hl7 each {v} $msg "PID.3.*.0.0" {
			lappend values $v
		}

		return $values
	} -result {ABC TST HEY}
}
